dnl Process this file with autoconf to produce a configure script.
dnl Example:
dnl		autoconf configure.in >configure
dnl

AC_REVISION($Id$)

AC_INIT(src/ipstatd.c)

AC_CONFIG_HEADER(include/autoconf.h)
AC_CANONICAL_HOST
AC_ARG_PROGRAM
AC_ARG_WITH(ipfilter,[
  --with-ipfilter	use ipfilter if it's available (default)
  --without-ipfilter	don't use ipfilter])
AC_ARG_WITH(ipchains,[
  --with-ipchains	use ipchains if it's available
  --without-ipchains	don't use ipchains
			these options work only for linux.])
AC_ARG_WITH(pcap,[
  --with-pcap		build libpcap version of ipstatd,
			normally this is last resort choice
  --without-pcap	don't compile in, libpcap support])


dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_CHECK_PROG(STRIP,strip,strip,strip,
	$PATH:/usr/bin:/usr/ccs/bin,/usr/local/bin/strip)
AC_PATH_PROG(SED,sed)
AC_PATH_PROG(AR,ar)
AC_CHECK_PROG(MKDEP,mkdep,mkdep)
AC_CHECK_PROG(MAKEDEPEND,makedepend,makedepend,,
	$PATH:/usr/openwin/bin:/usr/opt/imake/bin)

dnl Checks for libraries.
AC_SEARCH_LIBS(gethostbyname, nsl)
AC_SEARCH_LIBS(socket,socket nsl)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS( \
	fcntl.h \
	strings.h \
	sys/file.h \
	sys/ioctl.h \
	sys/time.h \
	syslog.h \
	signal.h \
	unistd.h \
	md5.h \
	netinet/ip_fil.h \
	netinet/ip_nat.h \
	netinet/ip_compat.h \
	netinet/ip_fil_compat.h
)

dnl Checks for typedefs.
AC_CHECK_TYPE(u_int32_t)
AC_CHECK_TYPE(u_int64_t)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)

if test "$ac_cv_type_u_int32_t" != yes ; then
	if test "$ac_cv_sizeof_int" = 4 ; then
		AC_MSG_RESULT(Define u_int32_t as unsigned int.)
		AC_DEFINE(u_int32_t,unsigned int)
	elif test "$ac_cv_sizeof_long" = 4 ; then
		AC_MSG_RESULT(Define u_int32_t as unsigned long.)
		AC_DEFINE(u_int32_t,unsigned long)
	else
		AC_MSG_ERROR([Unknown type u_int32_t.])
	fi
fi
if test "$ac_cv_type_u_int64_t" != yes ; then
	if test "$ac_cv_sizeof_long" = 8 ; then
		AC_MSG_RESULT(Define u_int64_t as unsigned long.)
		AC_DEFINE(u_int64_t,unsigned long)
	elif test "$ac_cv_sizeof_long_long" = 8 ; then
		AC_MSG_RESULT(Define u_int64_t as unsigned long long.)
  		AC_DEFINE(u_int64_t, unsigned long long)
	else
		AC_MSG_ERROR([Unknown type u_int64_t.])
	fi
fi

dnl Check for type in sys/socket.h
AC_CACHE_CHECK(for socklen_t, ac_cv_type_socklen_t, [
  AC_EGREP_CPP([socklen_t[^a-zA-Z_0-9]], [#include <sys/types.h>
#include <sys/socket.h>
#if STDC_HEADERS
#include <stdlib.h>
#include <stddef.h>
#endif],
    ac_cv_type_socklen_t=yes,
    ac_cv_type_socklen_t=no)
])
if test $ac_cv_type_socklen_t = no; then
	AC_MSG_RESULT(Define socklen_t as unsigned int.)
	AC_DEFINE(socklen_t, unsigned int)
fi

dnl Checks for structures.
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for compiler characteristics.
AC_C_BIGENDIAN
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS( select \
		socket \
		gethostbyname \
		snprintf \
		isblank )

dnl Checks for MD5 functions.
have_md5="yes"
if test "$ac_cv_header_md5_h" != yes ; then
	have_md5="no"
	AC_MSG_WARN([Can't find md5.h , will be build own libmd])
else
	AC_CHECKING(for MD5 functions in libc)
	AC_CHECK_FUNCS(MD5Init MD5Update MD5End,,[have_md5="no"; break])
	for lib in md md5 ; do
		if test "$have_md5" = no ; then
			AC_CHECKING(if system have $lib library)
			have_md5="yes"
			saved_LIBS="$LIBS"
			LIBS="$saved_LIBS -l$lib"
			unset ac_cv_func_MD5Init
			unset ac_cv_func_MD5Update
			unset ac_cv_func_MD5End
			AC_CHECK_FUNCS(MD5Init MD5Update MD5End,,[
				LIBS="$saved_LIBS"; have_md5="no"; break])
		fi
	done
	if test "$have_md5" = no ; then
		AC_MSG_WARN([Can't find MD5 libs , will be build own libmd])
	fi
fi
AC_SUBST(have_md5)
if test "$have_md5" = no ; then
	LIBS="$LIBS -lmd"
else
	AC_DEFINE(HAVE_MD5)
fi

dnl Check if IP-Filter headers are installed.
AC_MSG_CHECKING(if IP-Filter header files are installed)
if ( test "$ac_cv_header_netinet_ip_compat_h" = "yes" ||
	test "$ac_cv_header_netinet_ip_fil_compat_h" = "yes" ) &&
	test "$ac_cv_header_netinet_ip_fil_h" = "yes" &&
	test "$ac_cv_header_netinet_ip_nat_h" = "yes"  ; then
	HAVE_IPFILTER="yes"
else
	HAVE_IPFILTER="no"
fi
AC_SUBST(HAVE_IPFILTER)
AC_MSG_RESULT($HAVE_IPFILTER)
if test "$HAVE_IPFILTER" != "yes" ; then
    echo "WARNING: Cannot find necessary IP-Filter header files"
    echo "         ipstatd server will not be builded."
else
	AC_DEFINE(HAVE_IPFILTER)
fi

dnl Check if IP-Chains headers are installed.
case "$host_os" in
	linux*)
		# makedepend don't know anything about predefined macro, so
		# define linux explicitly.
		CFLAGS="$CFLAGS -Dlinux"
		AC_CHECK_HEADERS( \
			linux/ip.h \
			linux/tcp.h \
			linux/udp.h \
			linux/icmp.h \
			linux/if.h \
			linux/ip_fw.h
		)
		# Check if IP-Chains headers are installed.
		AC_MSG_CHECKING(if IP-Chains header files are installed)
		if test "$ac_cv_header_linux_ip_h"	= "yes" &&
		   test "$ac_cv_header_linux_tcp_h"	= "yes" &&
		   test "$ac_cv_header_linux_udp_h"	= "yes" &&
		   test "$ac_cv_header_linux_icmp_h"	= "yes" &&
		   test "$ac_cv_header_linux_if_h"	= "yes" &&
		   test "$ac_cv_header_linux_ip_fw_h" = "yes" ; then
		   AC_EGREP_CPP(IPCHAINS,
			[#include <linux/ip_fw.h>
			 #ifdef _IP_FWCHAINS_H
				IPCHAINS
			 #endif /* _IP_FWCHAINS_H */
			], HAVE_IPCHAINS="yes",HAVE_IPCHAINS="no")
		else
		   HAVE_IPCHAINS="no"
		fi
		AC_MSG_RESULT($HAVE_IPCHAINS)
		if test "$HAVE_IPCHAINS" != "yes" ; then
		   echo "WARNING: Cannot find necessary IP-Chains header files"
		   echo "         ipstatd server will not be builded."
		else
		   AC_DEFINE(HAVE_IPCHAINS)
		fi
		;;
	*)
		HAVE_IPCHAINS="no"
		;;
esac
AC_SUBST(HAVE_IPCHAINS)

dnl Checks for pcap library
AC_CHECK_HEADERS(pcap.h pcap/pcap.h)
if test "$ac_cv_header_pcap_h" = "yes" ||
   test "$ac_cv_header_pcap_pcap_h" = "yes" ; then
	AC_SEARCH_LIBS(pcap_open_live,pcap,HAVE_PCAP="yes",HAVE_PCAP="no")
	AC_SUBST(HAVE_PCAP)
	if test "$HAVE_PCAP" = "yes" ; then
		AC_DEFINE(HAVE_PCAP)
	fi
fi

AC_OUTPUT(Makefile src/Makefile lib/Makefile lib/md/Makefile)

